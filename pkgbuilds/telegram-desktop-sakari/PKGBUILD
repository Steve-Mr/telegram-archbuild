# Maintainer: Sakari <20642596+sakarie9@users.noreply.github.com>
# Contributor: lilydjwg <lilydjwg@gmail.com>
# Contributor: Megumi_fox <megumifox@archlinuxcn.org>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: hexchain <i@hexchain.org>
pkgname=telegram-desktop-sakari
_pkgname=telegram-desktop
pkgver=5.2.2
pkgrel=1
pkgdesc="Official Telegram Desktop client with megumifox & lily's patch"
arch=('x86_64')
url="https://desktop.telegram.org/"
license=('GPL-3.0-only WITH x11vnc-openssl-exception')
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal'
  'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash'
  'rnnoise' 'pipewire' 'libxtst' 'libxrandr' 'libxcomposite' 'libxdamage' 'abseil-cpp' 'libdispatch'
  'openssl' 'protobuf' 'glib2' 'libsigc++-3.0' 'kcoreaddons')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
  'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
  'glib2-devel' 'gobject-introspection' 'boost' 'fmt' 'mm-common' 'perl-xml-parser' 'python-packaging')
optdepends=('webkit2gtk: embedded browser features'
  'xdg-desktop-portal: desktop integration')
provides=('telegram-desktop')
conflicts=('telegram-desktop')
source=("https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
  "no-embolded-font.patch"
  "dont-adjust-fontsize.patch"
  "0001-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch"
  "0002-make-blockquote-markup-less-distractive.patch"
  "0003-Drop-usage-of-Qt-6.5-colorScheme-API-on-Linux.patch"
  "0004-make-animated-avatar-follow-power-settings.patch"
  "warn-before-delete-all-2.patch"
  "0009-Disable-sponsored-messages.patch"
  "0010-Add-option-to-disable-stories.patch")
sha512sums=('85fde17b0ea225e6ef7842688123381f1703aea460a4d4ecf46a8ac5d062c9d630140fca754c832e98925c71f4c6727233be418e88885e76d8f54386c826878e'
  '1c5ace38926fe8e2c2dfec6b0d9073c77031fc15b60220ffcad626a0f933d6d71d31bdb7e1ccb8bfaa76c1d0338ddd2193a2e75ac1623dd66bbc8e3ee9be4885'
  'a5cc0b821d493333ee71957e71be51fb753bb3c0b97ca44b63ee1f2c93fa5ea2d8dda0560a37cbe55bb9dd17b557ab8e95725cafe32e6bec1989bcd3f42945c6'
  'f1774ca438f2c32d4b8a593530e30f12c5ea225ac489cfc5602c88d81b1c4eee0d3514768364c682f3fa2f0bd0e6f6520c4c84c6b2ecbdb33def5b3737f7396a'
  'e472b945a6afdc43a48b6bf011b87ec19a9208583b410ec0213b7b16e5e5b21e66952f71974d0dce0ef6cb8c360ab9ed98f0bf9411dcd9c0440840ec2f7c2242'
  'bc1228eb3e65ece220b26460063189e5283d13f9e626f52737c464b65e32cd5975350754ee27ce1a67eccda6eb489776b6317e4a5746a3b27bb51494d6d0e27e'
  '970cb4f69c9420213825977a4711a6abf786c78d96737bba746d3946b1191cc79d59f3215f5ec5ccf1511b457850149b529b2966aeac58061058ab0fdaec920b'
  '891a58e7d1a4d4a27a17a2b56bd614d87d22322f2f6e1247665b2b873c5c6d78336de04bc7a11feceb7cd8f1a2f79a858ad2520352644e808634e2989a17357d'
  '70ede697fc328574e9d0f6403315df5ca3964e4ee9797672729f30242381ba1e726c72408c84b3dcedd0ccb230e8ddc346d0d7e64eb8155a21718af73c877fe0'
  '87d3dc4112dac5e87498f33edfa913f5606459000efb28a494257c178274a32a0ac955f6c02b972d45297f3439445d1d9826e85d06d439c02950fb23d0f740ab')

prepare() {
  patch -b -l -d tdesktop-$pkgver-full/Telegram/lib_ui -Np1 -i ${srcdir}/no-embolded-font.patch
  patch -b -l -d tdesktop-$pkgver-full/Telegram/lib_ui -Np1 -i ${srcdir}/dont-adjust-fontsize.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0001-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0002-make-blockquote-markup-less-distractive.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0003-Drop-usage-of-Qt-6.5-colorScheme-API-on-Linux.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0004-make-animated-avatar-follow-power-settings.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/warn-before-delete-all-2.patch

  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0009-Disable-sponsored-messages.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0010-Add-option-to-disable-stories.patch
}

build() {
  CXXFLAGS+=' -ffat-lto-objects'
  # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
  # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
  # Thanks @primeos!
  cmake -B build -S tdesktop-$pkgver-full -G Ninja \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_BUILD_TYPE=Release \
    -DTDESKTOP_API_ID=611335 \
    -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c \
    -DTDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
